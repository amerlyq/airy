pkgname=st-airy-git
_pkgname=st
pkgver=0.8.4
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
options=('zipman')
depends=('libx11' 'libxft' 'libxrender' 'libxrandr' 'fontconfig' 'freetype2')
makedepends=('ncurses' 'libxext' 'git')
epoch=1

# TRY:USE: https://savannah.nongnu.org/projects/quilt/ to track patch changes
source=('git://git.suckless.org/st#commit=2f6e597'
  st-spoiler-20180309-c5ba9c0.diff
  st-no_bold_colors-0.8.1.diff
  st-solarized-both-0.8.4.diff
  st-alpha-0.8.2.diff
  st-osc10-20210106-4ef0cbd.diff
  st-csi_22_23-0.8.4.diff
  st-hidecursor-0.8.3.diff
  st-externalpipe-0.8.2.diff
  st-newterm-0.8.2.diff
  my-tmux-0.8.4.diff

  st-boxdraw_v2-0.8.3.diff
  st-ligatures-boxdraw-20200430-0.8.3.diff

  ## SRC: https://github.com/LukeSmithxyz/st/pull/224
  #  from Azumgi/glyph_truncation_fix
  oth-glyph-truncation-fix.diff

  st-visualbell2-basic-2020-05-13-045a0fa.diff
  st-visualbell2-enhanced-2020-05-13-045a0fa.diff

  ## SRC: undercurl ⌇⡡⣅⢠⢪
  ## FIXED?(tmux): underline becomes corrupted (mixed white)
  #  * https://st.suckless.org/patches/undercurl/
  #  * https://github.com/tmux/tmux/issues/1492
  #  * https://github.com/tmux/tmux/pull/1771
  #  * https://github.com/neovim/neovim/issues/13734
  #  DEBUG: $ echo -e '\e[4:3m\e[58:2::240:143:104mfoobarbaz\e[59m\e[4:0m\n'
  ## FAIL: underline becomes corrupted: mixed white in tmux
  # st-undercurl-0.8.4-20210822.diff

  ## CASE: when monitors have different DPI
  my-xrandrfontsize-0.8.4-20211224-2f6e597.diff
  my-xkblangcolor-0.8.4-20211224-2f6e597.diff

  my-config-0.8.4.diff
)
# sha256sums=(SKIP SKIP SKIP SKIP SKIP SKIP SKIP SKIP SKIP)
sha256sums=(); for s in "${source[@]}"; do sha256sums+=(SKIP); done

provides=("${_pkgname}")
conflicts=("${_pkgname}")

pkgver() {
  cd "${_pkgname}"
  git describe --tags | sed 's/-/./g'
}

prepare() { local f
  cd "${_pkgname}"
  rm -f config.h
  # patch -p1 -i $srcdir/../st-$pkgver-argbbg.diff

  # sed -i '
  #   /char font/s/= .*/= "Fixed:pixelsize=13:style=SemiCondensed";/
  #   /char worddelimiters/s/= .*/= " '"'"'`\\\"()[]{}<>|";/
  #   /int defaultcs/s/= .*/= 1;/
  #   ' config.def.h
  # sed -i '
  #   s/CPPFLAGS =/CPPFLAGS +=/g
  #   s/CFLAGS =/CFLAGS +=/g
  #   s/LDFLAGS =/LDFLAGS +=/g
  #   s/_BSD_SOURCE/_DEFAULT_SOURCE/
  #   ' config.mk

  # FIXED:ERR: The next patch would create the file boxdraw.c, which already exists!  Skipping patch.
  rm -f -- boxdraw.c boxdraw_data.h hb.c hb.h

  for f in "${source[@]}"; do
    if [[ $f == *.diff || $f == *.patch ]]; then
      # NOTE: apply all patches
      printf '[%s]\n' "$f"
      patch -Np1 -i "$srcdir/$(basename "$f")"
    elif [[ $f == config.h ]]; then
      # NOTE: user's config.h as-is
      cp "$srcdir/$f" .
    fi
  done

  #%ONELINE: $ sed -ri '/char\s\*font\b/s/"[^"]+"/"monospace-12"/g' config.def.h
  # NEED: apply from outside before build
  # [[ -n ${FONT_ST-} ]] && sed -ri '/^static char \*font/s/("[ [:alpha:]]+-|:\s*(pixel)?size=)[0-9.]+/\1'"$FONT_ST"'/g' config.def.h

  ## SKIP terminfo: /usr/share/terminfo/s/st is owned by ncurses 6.0-4
  sed -i '/\@tic\s/d' Makefile
}

build() {
  cd "${_pkgname}"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${_pkgname}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
