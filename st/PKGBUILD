pkgname=st-airy-git
_pkgname=st
pkgver=0.7.9
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
options=('zipman')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
epoch=1

source=('git://git.suckless.org/st'
  no-bold.diff
  solarized-dual.diff
  alpha.diff
  tmux.diff
  hidecursor.diff
  externalpipe.diff
  kbd-input.diff
  config.diff
)
sha256sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

pkgver() {
  cd "${_pkgname}"
  git describe --tags | sed 's/-/./g'
}

prepare() { local f
  cd "${_pkgname}"
  rm -f config.h
  # patch -p1 -i $srcdir/../st-$pkgver-argbbg.diff

  # sed -i '
  #   /char font/s/= .*/= "Fixed:pixelsize=13:style=SemiCondensed";/
  #   /char worddelimiters/s/= .*/= " '"'"'`\\\"()[]{}<>|";/
  #   /int defaultcs/s/= .*/= 1;/
  #   ' config.def.h
  # sed -i '
  #   s/CPPFLAGS =/CPPFLAGS +=/g
  #   s/CFLAGS =/CFLAGS +=/g
  #   s/LDFLAGS =/LDFLAGS +=/g
  #   s/_BSD_SOURCE/_DEFAULT_SOURCE/
  #   ' config.mk

  for f in "${source[@]}"; do
    if [[ $f == *.diff || $f == *.patch ]]; then
      # NOTE: apply all patches
      patch -Np1 -i "$srcdir/$(basename "$f")"
    elif [[ $f == config.h ]]; then
      # NOTE: user's config.h as-is
      cp "$srcdir/$f" .
    fi
  done

  [[ -z ${FONT_ST-} ]] || sed -ri '/^char font/s/("[ [:alpha:]]+-|:\s*(pixel)?size=)[0-9.]+/\1'"$FONT_ST"'/g' config.def.h

  ## SKIP terminfo: /usr/share/terminfo/s/st is owned by ncurses 6.0-4
  sed -i '/\@tic\s/d' Makefile
}

build() {
  cd "${_pkgname}"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${_pkgname}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
