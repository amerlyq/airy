" vim:ts=2:sw=2:sts=2
if &compatible| set nocompatible |en

fun! _cfg(path)  " Must not contain '\n' in path. Compat globpath <v7.4.279
  for f in split(globpath($VIMHOME.'/cfg/', a:path), '\n')
    exe 'source' fnameescape(f)
  endfor
endf

" Set augroup for my vimrc autocmd
augroup MyAutoCmd
  autocmd!
augroup END

" ENH: cat all vimrc in cached version at $VCACHE
"   -- BUT some files contain 'finish' which obstructs merging
"   -- ALSO what to do with _cfg(...) commands left inside scripts?
" ENH: place some cfg in 'post' group to augment some commands/mappings
"   if their respectful plugin is disabled/absent or no plugins at all.

" NOTE: disables all '*/site' packages installed by pacman. SEE :h 'rtp'
set runtimepath=$VIMHOME,$VIMRUNTIME
call _cfg('core/*.vim')
call _cfg('core-cfg/*.vim')
call _cfg('features/'.(has('nvim')        ? 'neovim'  : 'vim'      ).'.vim')
call _cfg('features/'.(IsAndroid()        ? 'android' : 'clipboard').'.vim')
call _cfg('features/'.(IsWindows()        ? 'windows' : 'unix'     ).'.vim')
call _cfg('features/'.(has('gui_running') ? 'gui'     : 'term'     ).'.vim')
call _cfg('dein.vim')
call _cfg('core-post/*.vim')
set runtimepath+=$VIMHOME/after  " Place after dein#end()

filetype plugin indent on  " Required!
syntax enable  " 'enable' only sets groups not set yet. Use 'on' to overwrite

try
  if exists('$TMUX') && &t_Co <= 16
    color slate |else| color solarized
  endif
catch /^Vim\%((\a\+)\)\=:E185/
  color slate
endtry

" Exec .vimrc in curr dir, but deny itc :autocmd, :shell and :write cmds
set exrc secure modeline    " also parse 'vim:...' cmds from comments
