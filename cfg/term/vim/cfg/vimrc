" vim:ts=2:sw=2:sts=2
fun! _cfg(path)
  for f in globpath(expand($VIMHOME .'/cfg/'), a:path, 0, 1)
    exe 'source' fnameescape(f)
  endfor
endf

" Set augroup for my vimrc autocmd
augroup MyAutoCmd
  autocmd!
augroup END

call _cfg('core/*.vim')
call _cfg('core-cfg/*.vim')
call _cfg('features/'.(has('nvim')        ? 'neovim'  : 'vim'      ).'.vim')
call _cfg('features/'.(IsAndroid()        ? 'android' : 'clipboard').'.vim')
call _cfg('features/'.(IsWindows()        ? 'windows' : 'unix'     ).'.vim')
call _cfg('features/'.(has('gui_running') ? 'gui'     : 'term'     ).'.vim')
call _cfg('neobundle.vim')

filetype plugin indent on  " Required!
syntax enable

try
  if exists('$TMUX') && &t_Co <= 16
    color slate | else | color solarized
  endif
catch /^Vim\%((\a\+)\)\=:E185/
  color slate
endtry

" Exec .vimrc in curr dir, but deny itc :autocmd, :shell and :write cmds
set exrc secure modeline    " also parse 'vim:...' cmds from comments
