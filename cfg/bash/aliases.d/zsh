
## Global aliases
# Write L after a command to page through the output.
alias -g L="| less"
alias -g TL='| tail -20'
alias -g NUL="> /dev/null 2>&1" # You get the idea.

alias -g @="$(xsel -o)"


## Std-like
psg() { ps uwwp ${$(pgrep -d, "${(j:|:)@}"):?no matches}; }


# Show history
if [ "$HIST_STAMPS" = "mm/dd/yyyy" ]
then
    alias history='fc -fl 1'
elif [ "$HIST_STAMPS" = "dd.mm.yyyy" ]
then
    alias history='fc -El 1'
elif [ "$HIST_STAMPS" = "yyyy-mm-dd" ]
then
    alias history='fc -il 1'
else
    alias history='fc -l 1'
fi

## Expandable aliases
typeset -a ealiases
ealiases=()

ealias() {
    alias $1
    ealiases+=(${1%%\=*})
}

function expand-ealias() {
    if [[ $LBUFFER =~ "(^|[;|&])\s*(${(j:|:)ealiases})\$" ]]; then
        zle _expand_alias
        zle expand-word
    fi
    zle magic-space
}
zle -N expand-ealias

bindkey -M viins ' '    expand-ealias
bindkey -M viins '^ '   magic-space     # control-space to bypass completion
bindkey -M isearch " "  magic-space     # normal space during searches

ealias fff='find . -iname'
