
## Global aliases
# Write L after a command to page through the output.
alias -g L="| less"
alias -g G="| grep -i "
alias -g TL='| tail -20'
alias -g NUL="> /dev/null 2>&1" # You get the idea.
# alias -g NE='2>|/dev/null'
# alias -g NO='&>|/dev/null'
# alias -g PG='|$PAGER'

# alias -r @="printf \"$(xsel -ob)\n\""
alias -g @="\"$(xsel -ob)\""


## Std-like
psg() { ps uwwp ${$(pgrep -d, "${(j:|:)@}"):?no matches}; }

  if (( $+commands[links2] )) ; then export LINKS_BROWSER="links2"
elif (( $+commands[links] )) ; then export LINKS_BROWSER="links"
elif (( $+commands[lynx] )) ; then export LINKS_BROWSER="lynx"
fi

alias myip=$LINKS_BROWSER' -dump http://www.myip.ch/ | grep -o "[[:digit:]].*[[:digit:]]"'

# Colored less in pipe
# http://www.greenwoodsoftware.com/less/faq.html
# https://wiki.archlinux.org/index.php/core_utilities#Colored_output_when_reading_from_stdin
# zmodload zsh/zpty
# pty() {
#     zpty pty-${UID} ${1+"$@"}
#     if [[ ! -t 1 ]];then
#         setopt local_traps
#         trap '' INT
#     fi
#     zpty -r pty-${UID}
#     zpty -d pty-${UID}
# }
# ptyless() {
#     pty $@ | less
# }

# Show history
# Uncomment the following line if you want to change the command execution
# time stamp shown in the history command output.
# The optional three formats: "mm/dd/yyyy"|"dd.mm.yyyy"|"yyyy-mm-dd"
# HIST_STAMPS="mm/dd/yyyy"
case "$HIST_STAMPS" in
    "mm/dd/yyyy") alias history='fc -fl 1' ;;
    "dd.mm.yyyy") alias history='fc -El 1' ;;
    "yyyy-mm-dd") alias history='fc -il 1' ;;
               *) alias history='fc -l 1'  ;;
esac

## Expandable aliases
typeset -a ealiases
ealiases=()

ealias() {
    alias $1
    ealiases+=(${1%%\=*})
}

function expand-ealias() {
    if [[ $LBUFFER =~ "(^|[;|&])\s*(${(j:|:)ealiases})\$" ]]; then
        zle _expand_alias
        zle expand-word
    fi
    zle magic-space
}
zle -N expand-ealias

bindkey -M viins ' '    expand-ealias
bindkey -M viins '^ '   magic-space     # control-space to bypass completion
bindkey -M isearch " "  magic-space     # normal space during searches

ealias fff='find . -iname'
