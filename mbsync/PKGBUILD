# NOTE: git clone of official package

pkgname=isync-git
pkgver=1.3.1
pkgrel=1
pkgdesc="IMAP and MailDir mailbox synchronizer"
arch=('i686' 'x86_64')
url="http://isync.sourceforge.net"
license=('GPL2')
depends=('libsasl' 'zlib')
conflicts=('isync')
source=('git+https://git.code.sf.net/p/isync/isync' davmail.diff)
md5sums=(SKIP SKIP)

pkgver() {
  cd isync
  echo "$(git describe --abbrev=0 --tags|cut -d- -f2|sed s/v//).r$(git rev-list --count HEAD).$(git log -1 --format=g%h)"
}

prepare() {
  cd isync
  patch -Np1 -i "$srcdir"/davmail.diff
}

build() {
  cd isync
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd isync
  make DESTDIR="$pkgdir" install
}
