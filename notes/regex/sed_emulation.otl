Here's the list of some UNIX commands that can be emulated
using SED. Please, if know about others, contribute!

Color
	toto pointed out that the \x1b character is the escape character:
		echo "foo color bar" | sed -e "s/color/\x1b[7m&\x1b[0m/"

Replace in many files
	> grep -Rl '\(level-\?[>.]\)\([ab]\)\(\W\)' . |
		# xargs sed -i '/\(level-\?[>.]\)\([b]\)\(\W\)/ s//\1width\3/g'
		# xargs sed '/\(level-\?[>.]\)\([a]\)\(\W\)/!d; s//\1height\3/g'
		# xargs sed -i '/\(level-\?[>.]\)\([a]\)\(\W\)/ s//\1height\3/g'
		xargs sed '/\(level-\?[>.]\)\([ab]\)\(\W\)/!d; \
		  s//\x1b[31m\1\x1b[32m\2\x1b[31m\3\x1b[0m/g'


| UNIX         |  SED
|--------------+---------------------------------------------------------------
| cat          |  sed ':'
| cat -s       |  sed '1s/^$//p;/./,/^$/!d'
| tac          |  sed '1!G;h;$!d'
| grep         |  sed '/patt/!d'
| grep -v      |  sed '/patt/d'
| head         |  sed '10q'
| head -1      |  sed 'q'
| tail         |  sed -e ':a' -e '$q;N;11,$D;ba'
| tail -1      |  sed '$!d'
| tail -f      |  sed -u '/./!d'
| cut -c 10    |  sed 's/\(.\)\{10\}.*/\1/'
| cut -d: -f4  |  sed 's/\(\([^:]*\):\)\{4\}.*/\2/'
| tr A-Z a-z   |  sed 'y/ABCDEFGHIJKLMNOPQRSTUVWXYZ/abcdefghijklmnopqrstuvwxyz/'
| tr a-z A-Z   |  sed 'y/abcdefghijklmnopqrstuvwxyz/ABCDEFGHIJKLMNOPQRSTUVWXYZ/'
| tr -s ' '    |  sed 's/ \+/ /g'
| tr -d '\012' |  sed 'H;$!d;g;s/\n//g'
| wc -l        |  sed -n '$='
| uniq         |  sed 'N;/^\(.*\)\n\1$/!P;D'
| rev          |  sed '/\n/!G;s/\(.\)\(.*\n\)/&\2\1/;//D;s/.//'
| basename     |  sed 's,.*/,,'
| dirname      |  sed 's,[^/]*$,,'
| xargs        |  sed -e ':a' -e '$!N;s/\n/ /;ta'
| paste -sd:   |  sed -e ':a' -e '$!N;s/\n/:/;ta'
| cat -n       |  sed '=' | sed '$!N;s/\n/ /'
| grep -n      |  sed -n '/patt/{=;p;}' | sed '$!N;s/\n/:/'
| cp orig new  |  sed 'w new' orig
| hostname -s  |  hostname | sed 's/\..*//'

Remove sections from file
:Silent cat sim.otl | sed "/Category/,/Description/d" | sed -n "/NOT/,/^$/p" > ./tmp.otl
