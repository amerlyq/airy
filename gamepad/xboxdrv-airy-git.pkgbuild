pkgname=xboxdrv-airy-git
_pkgname=xboxdrv
pkgver=0.8.8
pkgrel=4
pkgdesc="An XBox/XBox 360 gamepad driver - as alternative to the xpad-kernel module - with more configurability, runs in userspace and supports a multitude of controllers"
arch=('i686' 'x86_64')
# url="https://xboxdrv.gitlab.io"
# url="http://pingus.seul.org/~grumbel/xboxdrv/"
# REF: https://github.com/RetroPie/RetroPie-Setup/issues/1277
url="https://github.com/zerojay/xboxdrv/"
license=('GPL3')
depends=('libx11' 'dbus-glib' 'libusbx')
makedepends=('git' 'scons' 'boost' 'pkg-config' 'libx11' 'dbus-glib' 'libusb')
provides=('xboxdrv')
conflicts=('xboxdrv')

source=('git+https://github.com/zerojay/xboxdrv.git#branch=stable'
  patches/add-xbox-one-elite.patch
  patches/xboxdrv.service
  patches/xboxdrv.conf
)
sha256sums=('SKIP'
            'd4d7bcf54d41ee691b64f0a8cba3fe763f1eda6c0cf331abf8019ae1bac61593'
            '8c9f7c384af94a4aade654390fe8ccec56d1c0d4f700f7f0a676b8fbddcf3faa'
            'd203d7dc61e446353035944027597b57a4a02cfd2b60454d905eb263baf2efad')


pkgver() {
  cd $_pkgname
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $_pkgname
  msg "Building..."
  for f in "${source[@]}"; do
    [[ $f == *.patch ]] && patch -Np1 -i "$srcdir/${f##*/}"
  done
  make
}

package() {
  cd $_pkgname
  make install PREFIX=/usr DESTDIR=$pkgdir

  install -D -m755 $srcdir/xboxdrv.service $pkgdir/usr/lib/systemd/system/xboxdrv.service
  install -D -m644 $srcdir/xboxdrv.conf $pkgdir/etc/conf.d/xboxdrv
}
