#!/usr/bin/env -S gawk -f
# vim:ft=awk
BEGIN {
  IGNORECASE = 1

  # HACK: do not print "^O" in "less" (crop last char)
  cmd = "tput sgr0"; cmd | getline CR; close(cmd)
  CR  =  substr(CR, 1, length(CR) - 1)

  cmd = "tput bold"; cmd | getline CB; close(cmd)  # remove: $ tput sgr0
  cmd = "tput smul"; cmd | getline CU; close(cmd)  # remove: $ tput rmul
  cmd = "tput sitm"; cmd | getline CI; close(cmd)  # remove: $ tput ritm

  for(i = 0; i < 256; ++i) {
    cmd = "tput setaf " i
    cmd | getline C[i]
    close(cmd)
  }
}

# log level
$3 == "error"{ $3 = CB C[1] $3 CR }
$3 == "warn" { $3 = CB C[3] $3 CR }
$3 == "info" { $3 = CB C[6] $3 CR }

# log TID/PID
{
  # WARN: 256 colors don't work through tmux+SSH
  c = C[($5 % 100) * 2 + 22]
  $4 = c $4 CR
  $5 = c $5 CR
}

# chosen words
{
  gsub(/\<\[(watchdog|dbstatus)\]\>/, C[13] "&" CR)
  gsub(/\<can('t|not)\>/, C[9] "&" CR)
  gsub(/\<must\>/, C[5] "&" CR)
  gsub(/\<(exception|errors?)\>/, CB C[1] "&" CR)
  gsub(/\<dbpath\>/, C[4] "&" CR)
  gsub(/[0-9]{2}(:[0-9]{2}){5}/, C[7] "&" CR)
  gsub(/\s[A-Za-z]\w+(:\s|=)/, CI C[3] "&" CR)
  gsub(/[ =][0-9]+[ ,]/, C[2] "&" CR)
  # gensub(/([^|])\<([0-9]+)\>/, "\\1" C[2] "\\2" CR, "g")
}

{ print }
