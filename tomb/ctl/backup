#!/usr/bin/env zsh
#
# SPDX-FileCopyrightText: 2020 Amerlyq <amerlyq@gmail.com>
#
# SPDX-License-Identifier: GPL-3.0-only
#
#%SUMMARY: sync local ~/aura repos with remote tomb container
#%BET: sync all tombs first -- and only then use "Gp" for rest of ~/aura repos
#%USAGE: $ ./$0 [-R .] [--] [~/aura/todo] ...
set -o errexit -o noclobber -o noglob -o nounset -o pipefail

# DEV:ENH: convert to global r.git-repo tool to support all my usecases
#   OR:TRY: use existing myrepo tool -- maybe its manifest system will do nicely

# WARN:TODO: if container will be ever corrupted/deleted
#   -- how to easily restore it from repos structure on either PC ?

unset GIT_DIR
grpnm='tomb'
d_keys=/aura/archive/$grpnm
d_sshfs=lc:/me/$grpnm  # MOVE:(direct symlink): -> "lc:/tomb"
d_tombs=/srv/$grpnm/${d_sshfs%%:*}
d_mnt=/mnt
declare -A online_rmt

tomb='aura'  # FIXME? mandatory argument

#%USAGE:(local): $ ./$0 -S '' -T /tmp/tomb -n mytomb -m
choices=mcsu
opts=; while getopts "AK:M:n:RS:T:v$choices" o; do case $o in :|'?') exit 1
;; A) opts+=$choices
;; n) tomb=$OPTARG
;; K) d_keys=$OPTARG
;; M) d_mnt=$OPTARG
;; S) d_sshfs=$OPTARG
;; T) d_tombs=$OPTARG
esac; opts+=$o; done; shift $((OPTIND - 1))
# ALT:(generic: -F => -Fmcsu): [[ -z ${opts//[^R$choices]} ]] && opts+=$choices
[[ -z ${opts//[R]} ]] && opts+=$choices  # =DFL
dm=$grpnm-$tomb

has(){ [[ $opts =~ [$*] ]]; }
git(){ command git -C ${GIT_DIR:?} --no-pager -c color.status=always -c color.ui=always "$@"; }

function CollectAuraRepos { local d r url
  typeset -ga repos
  set +o noglob
  for d; do
    for r in "$d"/*; do
      [[ -L $r && -d $r/.git ]] || continue
      local GIT_DIR=$r
      # DEPR:(fallback): url=$(git remote get-url "$tomb" || git remote get-url origin ||:) 2>/dev/null
      url=$(git remote get-url "$tomb" ||:) 2>/dev/null
      [[ $url == file:///mnt/* ]] && repos+=( "$r" )
      true
    done
  done
  set -o noglob
}

function expectMount { local expected=$((!!$1)) dev=$2 src=${3-} tgt=${4-}
  if ((expected)); then
    [[ $dev == $src ]] && return 0
    [[ -z $dev ]] && >&2 echo "Warn: not mounted=$tgt" && return 1
  else
    [[ -z $dev ]] && return 0
    [[ $dev == $src ]] && >&2 echo "Warn: already mounted=$dev" && return 1
  fi
  >&2 echo "Err: wrong mount=$dev"
  exit 1
}

function notMounted { local src=$1 tgt=$2  # ok=${3:-1} err=0
  expectMount 0 "$(findmnt -nfo SOURCE -M $tgt)" $src $tgt
}
function isMounted { local src=$1 tgt=$2
  expectMount 1 "$(findmnt -nfo SOURCE -M $tgt)" $src $tgt
}

function notOpened { local luks=$1 dm=$2 loop
  # ALT: cryptsetup isLuks $luks && test -b /dev/disk/by-id/dm-uuid-*$(cryptsetup luksUUID $luks | tr -d -)*
  # ALT: dmsetup ls | grep $dm | cut -f1 -d_
  # BAD: don't work w/o "sudo" even for idle run
  loop=$(sudo cryptsetup status $dm | sed -rn '/^\s*loop:\s*(.*)/s//\1/p') || (($?==4?0:$?))
  expectMount 0 "$loop" $luks $dm
}
function isOpened { local luks=$1 dm=$2 loop
  loop=$(sudo cryptsetup status $dm | sed -rn '/^\s*loop:\s*(.*)/s//\1/p') || (($?==4?0:$?))
  # BUG: loop=/aura.luks != /srv/tomb/lc/aura.luks
  #   FIXED: sudo cryptsetup close -- tomb-aura
  # FIXED:(:P): use realpath(orgn) to compare with loop path
  expectMount 1 "$loop" ${luks:P} /dev/mapper/$dm
}


# HACK: if this device is already mounted --
#   it means git-syncup was interrupted with errors,
#   so skip and continue git-syncup again
function OpenTomb { local rloc=$1 loc=$2 dm=$3 mnt=$4

  if [[ -n $rloc ]] && notMounted $rloc $loc; then
    sudo install -m 700 -o $USER -g $USER -d -- $loc
    ## FAIL: can't enter password into background process
    #    [⡟⡸⣆⢓] WTF: why I need to enter password for authkey ssh in the first place ?
    #      ::: FIXED: $ ssh-copy-id -i ~/.ssh/id_rsa.pub lc && ssh lc 'tail -1 ~/.ssh/authorized_keys >> /_cfg/ssh/authorized_keys'
    # sshfs -f --debug -o "umask=0077,noexec,no_readahead,allow_root" $rloc $loc &
    sshfs -o "umask=0077,noexec,no_readahead,allow_root" $rloc $loc
  fi

  local mode=rw
  local luks=$loc/$tomb.luks
  [[ ! -r $luks ]] && >&2 echo "Err: luks='$luks' file must exist and be readable" && exit 1
  [[ ! -w $luks ]] && mode=ro && >&2 echo "Warn: no write permissions, luks='$luks' file will be mounted readonly"
  # CHECK: maybe allow writing into symlinks after above fix of "${luks:P}"
  if [[ -w $luks && -L $luks ]]; then
    >&2 echo "Err: writable luks='$luks' must not be symlink. Hint: \$ git annex unlock ${luks##*/}"
    exit 1
  fi

  if notOpened $luks $dm; then
    local lock=${luks:r}.lock
    [[ -e $lock ]] && >&2 echo "Err: exists lock='$lock' ($(<$lock)), never open tomb from two hosts!" && exit 1

    # BAD: actual pass path directly depends on $rloc
    #   << dif. servers may contain same LUKS container copy with dif. passwd
    #   TEMP:TRY: keep unique names for all containers
    local pass=$grpnm/$tomb
    local keys=$d_keys/$tomb.keys
    # BUG: newline is part of password
    pass $pass | sudo cryptsetup --key-file - luksOpen --header $keys -- $luks $dm
    # FAIL:FIXME: if dst location is RO
    print -l "$HOST" > "$lock"
  fi

  local dev=/dev/mapper/$dm
  if notMounted $dev $mnt; then
    sudo mount -o "$mode" $dev $mnt
  fi
}

# TODO: call CloseTomb on demand by flag
function CloseTomb { local rloc=$1 loc=$2 dm=$3 mnt=$4
  sync

  # FIXME: unmount only single $loop path from notOpened()
  # HACK: close any stray loop device, which prevents unmounting
  # FAIL: will close all opened loop devices, even unrelated
  #   BUT:NICE: nested tombs are mutually exclusive anyways
  losetup -a | cut -d: -f1 | sort -n | tail -1 | xargs -r sudo losetup -d

  local dev=/dev/mapper/$dm
  local luks=$loc/$tomb.luks
  # MAYBE: I can't check "isOpened" after unmounting "dev" based on "dm"
  isMounted $dev $mnt && sudo umount $mnt
  if isOpened $luks $dm; then
    sudo cryptsetup close -- $dm
    local lock=${luks:r}.lock
    [[ ! -e $lock ]] && >&2 echo "Warn: expected lock='$lock' does not exist!"
    local host=$(<"$lock")
    [[ $host != "$HOST" ]] && >&2 echo "Warn: lock='$lock' contains wrong host='$host'" && exit 1
    rm -- $lock
  fi
  if [[ -n $rloc ]]; then isMounted $rloc $loc && fusermount -u -- $loc; fi

  # ALT: wait until background sshfs exits
  # wait
}

function SyncRepos {  local r
  for r; do
    local GIT_DIR=$r
    # FIXED: allow executing !r.tomb-backup from repo subfolder
    [[ -e $r ]] && r=$(git rev-parse --show-toplevel)

    r.git-status $r
    ## ATT: may error-out due to merge conflict => run rest of commands yourself
    # FAIL: fetches all remotes
    #   FIXME: fetch only specific remote "$tomb" or default one "origin"
    # git-up $r origin 2>&1

    local rmt url srv="file:///mnt/${${r:A:t}#.}.git"
    # DEPR:(fallback): origin ...
    for rmt in $tomb; do
      url=$(git remote get-url "$rmt" 2>/dev/null ||:)
      [[ -z $url ]] && continue
      [[ $url == "$srv" ]] && break || unset rmt
      [[ $url == file:///mnt/* ]] && >&2 echo "Err: remote='$rmt' points to wrong srv='$url'" && exit 1
    done
    [[ -z ${rmt-} ]] && >&2 echo "Err: private remote=${rmt:-TBD} not found" && exit 1

    # HACK: sync annex only available remotes
    #   IMPL: if some remotes are not available -- disable them for whole session
    # OFF:WiP: learn about remotes that are currently unavailable ⌇⡟⣻⣳⡌
    #   https://git-annex.branchable.com/todo/learn_about_remotes_that_are_currently_unavailable/
    local _nm _url _annex=""
    for _nm in $(git remote show); do
      _url=$(git remote get-url "$_nm")
      if ((!$+online_rmt["$_url"])); then
        if git ls-remote --quiet --exit-code --heads -- "$_nm" &>/dev/null
        then online_rmt["$_url"]=1
        else online_rmt["$_url"]=0
        fi
      fi
      (($online_rmt["$_url"])) || _annex+=" -c remote.$_nm.annex-sync=false"
    done
    [[ -n ${_annex:+x} ]] && >&2 echo "DISABLED:(annex-args): $_annex"

    if [[ -d $r/.git/annex/objects ]]; then
      git ${=_annex} annex $(has v&&echo --verbose) sync --no-commit --not-only-annex --content --pull --no-push
    else
      local br
      br=$(git symbolic-ref --short HEAD)
      # THINK:ALSO: --recurse-submodules=yes
      # git fetch --verbose --tags --prune -- "$rmt"
      git pull $(has v&&echo --verbose) --tags --autostash --rebase=merges -- "$rmt" "$br"
      # BUG? overrides beatrix push for "local" -> "aura/master" instead of "aura/local"
      #   --set-upstream
    fi

    local fetched
    # FIXME:(@{1}): newly cloned repos have only @{0} entry in reflog
    #   ERR: fatal: log for 'master' only has 1 entries
    fetched=$(git rev-list '@{1}..' --ignore-submodules --count || echo 0)
    if ((fetched)); then
      r.git-status $r
    fi

    # local err
    # BAD: pushes all BUT: must push "aura" and (only if matching) origin
    # r.git-push $r 2>&1 || ((1==(err=$?))) || exit $err

    # [_] FIXME! push only branches which !point to $tomb remote!
    # FAIL?(nonintuitive): pushes "--all" branches only if "current" branch has commits
    local rbranch ahead
    rbranch=$(git rev-parse --abbrev-ref --symbolic-full-name '@{u}')
    ahead=$(git rev-list HEAD..."$rbranch" --ignore-submodules --count)
    if ((ahead)); then
      # FIXME:(sanity-check): exit with error (don't push)
      #   * if any (still local) commit has prefix ~ /^(fix|fixup|tmp|temp):|^.?\w+!/
      ## MAYBE:ALSO: --set-upstream "$rmt"

      ## ERR: remote: 'receive.denyCurrentBranch' configuration variable to 'refuse'.
      #   To ssh://rmt/data/repo
      #   ! [remote rejected]   master -> master (branch is currently checked out)
      #   error: failed to push some refs to 'ssh://rmt/data/repo'
      ##  ::: FIXED:WKRND: use !gas on remote system and then !gas again on local -- to sync "master" changes by pulling
      if [[ -d $r/.git/annex/objects ]]; then
        git ${=_annex} annex $(has v&&echo --verbose) sync --no-commit --not-only-annex --content --no-pull --push
      else
        git push $(has v&&echo --verbose) --atomic --all
        git push $(has v&&echo --verbose) --atomic --tags
      fi

      r.git-status $r
    fi
    printf "==================\n\n"
  done
}

function CfgCloneCentralRepos { local r
  for r; do
    local GIT_DIR=$r

    ## TODO: clone root-repo and all submodules recursively
    # root=$(git rev-parse --show-superproject-working-tree --show-toplevel | head -1)
    # git -C "$root" git submodule foreach ...

    # FIXED: allow executing !r.tomb-backup from repo subfolder
    [[ -e $r ]] && r=$(git rev-parse --show-toplevel)

    local nm=${${r:A:t}#.}
    local mnt=/mnt/$nm.git
    local srv="file://$mnt"
    local rmt=$tomb

    # TRY:SRC:(NF): test non-empty-dir by ZSH: $ /path/to/dir(NF)
    #   https://stackoverflow.com/questions/91368/checking-from-shell-script-if-a-directory-contains-files
    # [_] CHECK: how GIT_DIR will work with git clone
    [[ -e $r ]] || git clone --origin "$rmt" -- "$srv" "$r"
    # [_] TODO:ALSO:(for git-annex-sync): $ gmy -m
    [[ -e $mnt ]] || git clone --bare --mirror -- "file://$r:A" "$mnt"

    # NOTE: influence all repos with (origin|aura) =~ file:///mnt/*
    local url
    url=$(git remote get-url "$rmt" 2>/dev/null ||:)
    [[ -z $url ]] && git remote add "$rmt" "${url:=$srv}"
    [[ $url != "$srv" ]] && git remote set-url "$rmt" "$srv"

    # DEPR:(fallback): don't create "origin" if you have "aura" (NICE: to preserve STD:git:WF)
    #   OR~~ don't create "aura" if you already have "origin"
    #   NICE:(both): prevent duplicate maintenance
    #   E.G.BAD: !git-annex syncs ALL branches (twice)
    # local orn rmt=origin
    # orn=$(git remote get-url origin 2>/dev/null ||:)
    # [[ -z $orn ]] && git remote add origin "${orn:=$srv}"
    # [[ $orn == file:///mnt/* && $orn != "$srv" ]] && git remote set-url origin "$srv"

    local ups
    ups=$(git rev-parse --abbrev-ref --symbolic-full-name '@{u}' 2>/dev/null ||:)
    # BAD:(confusing code): fallback to "$rmt" for DFL branch tracking
    if [[ -z $ups ]]; then
      git fetch $(has v&&echo --verbose) --tags --prune -- "$rmt"
      # ALG:BUG: "git fetch --set-upsteam" seems does not work
      #  [_] WTF: still not tracking -- remotes are not shown until $ gfA
      local br
      br=$(git symbolic-ref --short HEAD)  # OR:USE(ups): "$b@{u}"
      # OR: git remote add --track master // git remote add -f -t master -m master "$nm" "$url"
      # [_] MAYBE: always replace default remote of "master" branch ?
      git branch --set-upstream-to="$rmt/$br" "$br"
    fi
  done
}


if has R; then  #% -R = scan ~/aura and gather all repos with (origin|aura) => /mnt
  (($#)) || set -- '/@'
  CollectAuraRepos $@
else
  repos=( "$@" )
fi

has s && ((!$#repos)) && >&2 echo "Err: empty repos list" && exit 1
has v && print -l $repos

has m && OpenTomb "$d_sshfs" "$d_tombs" "$dm" "$d_mnt"
has c && CfgCloneCentralRepos $repos
has s && SyncRepos $repos
# MAYBE: trap '...' EXIT ERR
has u && CloseTomb "$d_sshfs" "$d_tombs" "$dm" "$d_mnt"
exit 0
