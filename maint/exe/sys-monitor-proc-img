#!/usr/bin/gnuplot -c
#%FORMAT:(in): [ts %cpu mem]
#%USAGE: $ ./$0 sys.perf
#%LICENSE: MIT, (c) amerlyq, 2019
#%ALT: [oneliner]: $ <sys.perf gnuplot -p -e 'N="ts,sec %cpu(total) %mem(pss)"' -e 'set xlabel word(N,1); set ylabel "ratio,%"' -e "plot for [i=2:3] '/dev/stdin' using 1:i with lines lw 2 title word(N,i) noenhanced"

#%USAGE: comment-out to open preview window
set term png size 1024, 600
set output ARG1 . '.png'

set size ratio 0.5
set size 1,1
set xlabel "t, s"
# NOTE: remove large empty area on left and right
set xrange [8307:8329]

set ytics 10 nomirror autofreq tc lt 1
set ylabel "cpu, %"
set yrange [0:*]

set y2tics 10 nomirror autofreq tc lt 2
set y2label "mem(pss), MB" tc lt 2
# NOTE: adjust memory range for better resolution
set y2range [9:10]

plot ARG1 using 1:2 with lines lw 2 lt 1 title "cpu", \
     ARG1 using 1:3 with lines lw 2 lt 2 title "mem" axes x1y2
